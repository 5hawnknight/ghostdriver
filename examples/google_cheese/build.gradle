task wrapper(type: Wrapper) {
    gradleVersion "1.0-rc-3"
}

apply plugin: "java"

repositories {
    mavenCentral()
}

ext.seleniumVersion = "2.21.0"

dependencies {
    ["selenium-java", "selenium-remote-driver", "selenium-server"].each {
        compile "org.seleniumhq.selenium:$it:$seleniumVersion"
    }
}

tasks.withType(JavaExec) {
    classpath = configurations.compile + sourceSets.main.output
    args project.hasProperty("args") ? project.args.split("\\s") : []
}

// ./gradlew exec [-Pargs="firefox|chrome|ghostdriver|stats"]
task exec(type: JavaExec) {
    main "ghostdriver.GoogleCheese"
}

// ./gradlew execServer [-Pargs="NUM_ITERATIONS SERVER_PORT"]
task execServer(type: JavaExec) {
    main "ghostdriver.GoogleCheeseServerOnly"
}
